/*
    An HTML grammar for use with TDParseKit

    this grammar is intentionally very forgiving (non-strict)
    the point here is to highlight, not validate, HTML.
*/

@multiLineComments = '<!--' '-->';
@commentState = '<';
@commentState.fallbackState = delimitState;

@delimitedString = '<?' '?>' nil;
@delimitedString = '<!DOCTYPE' '>' nil;
@delimitedString = '<![CDATA[' ']]>' nil;
@delimitState.fallbackState = symbolState;

@start          = any*;
any             = element | text | doctype | cdata | pi | comment;

pi              = DelimitedString('<?', '?>');
cdata           = DelimitedString('<![CDATA[', ']]>');
doctype         = DelimitedString('<!DOCTYPE', '>');

element         = emptyTag | startTag elementContent* endTag;
elementContent  = element | text | comment | pi | cdata;
text            = /[^<]+/;

emptyTag        = lt tagName attr* fwdSlash gt;
startTag        = lt junk? tagName attr* gt;
endTag          = lt fwdSlash tagName gt;

qName           = qualifiedName | unqualifiedName;
qualifiedName   = prefix colon unqualifiedName;
unqualifiedName = Word;

prefix          = Word;
suffix          = Word;
colon           = ':';

tagName         = qName;

attr            = attrName (eq attrValue)?;
attrName        = qName;
attrValue       = QuotedString;

eq              = '=';
lt              = '<';
gt              = '>';
fwdSlash        = '/';

comment         = Comment;
